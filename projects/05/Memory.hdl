// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including RAM, memory mapped screen, and memory mapped keyboard.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 (0x0000 to 0x3FFF) results in accessing the RAM;
 * Access to address 16384 to 24575 (0x4000 to 0x5FFF) results in accessing the Screen memory map;
 * Access to address 24576 (0x6000) results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    DMux4Way(in=load, sel=address[13..14],
             a=ramload0, b=ramload1, c=screenload, d=discard);
    Or(a=ramload0, b=ramload1, out=ramload);

    /* use address of 0b000000000000000 ~ 0b011111111111111 */
    RAM16K(in=in, load=ramload, address=address[0..13], out=ramout);
    /* use address of 0b100000000000000 ~ 0b101111111111111 */
    Screen(in=in, load=screenload, address=address[0..12], out=screenout);
    /* use address of 0b110000000000000 ~ 0b110000000000001 */
    Keyboard(out=kbdout);

    Mux4Way16(a=ramout, b=ramout, c=screenout, d=kbdout,
              sel=address[13..14], out=out);
}
